pluginManagement {
  repositories {
    def artifactoryPassword = System.env.'ARTIFACTORY_ACCESS_TOKEN'

    maven {
      if (artifactoryPassword) {
        url 'https://repox.jfrog.io/repox/plugins.gradle.org/'
        authentication {
          header(HttpHeaderAuthentication)
        }
        credentials(HttpHeaderCredentials) {
          name = "Authorization"
          value = "Bearer $artifactoryPassword"
        }
      } else {
        url 'https://plugins.gradle.org/m2/'
      }
    }
  }
}
plugins {
  id("com.gradle.develocity") version("4.0.2")
  id("com.gradle.common-custom-user-data-gradle-plugin") version "2.4.0"
}

rootProject.name = 'sonarqube'

include 'plugins:sonar-xoo-plugin'
include 'plugins:sonar-education-plugin'

include 'server:sonar-auth-common'
include 'server:sonar-auth-bitbucket'
include 'server:sonar-auth-github'
include 'server:sonar-auth-gitlab'
include 'server:sonar-auth-ldap'
include 'server:sonar-auth-saml'
include 'server:sonar-ce'
include 'server:sonar-ce-common'
include 'server:sonar-ce-task'
include 'server:sonar-ce-task-projectanalysis'
include 'server:sonar-db-core'
include 'server:sonar-db-dao'
include 'server:sonar-db-migration'
include 'server:sonar-main'
include 'server:sonar-process'
include 'server:sonar-server-common'
include 'server:sonar-statemachine'
include 'server:sonar-telemetry'
include 'server:sonar-telemetry-core'
include 'server:sonar-webserver'
include 'server:sonar-webserver-api'
include 'server:sonar-webserver-auth'
include 'server:sonar-webserver-common'
include 'server:sonar-webserver-core'
include 'server:sonar-webserver-es'
include 'server:sonar-webserver-webapi'
include 'server:sonar-webserver-webapi-v2'
include 'server:sonar-webserver-pushapi'
include 'server:sonar-webserver-ws'
include 'server:sonar-alm-client'
include 'server:sonar-webserver-monitoring'

include 'sonar-application'
include 'sonar-core'
include 'sonar-sarif'
include 'sonar-duplications'
include 'sonar-markdown'
include 'sonar-plugin-api-impl'
include 'sonar-scanner-engine'
include 'sonar-scanner-engine-shaded'
include 'sonar-scanner-protocol'
include 'sonar-shutdowner'
include 'sonar-testing-harness'
include 'sonar-testing-ldap'
include 'sonar-ws'
include 'sonar-ws-generator'

ext.isCiServer = System.getenv().containsKey("GITHUB_ACTIONS")

develocity {
  projectId = "sonar-enterprise"
  server = "https://develocity.sonar.build"
  buildScan {
    uploadInBackground.set(!isCiServer)
    publishing.onlyIf { isCiServer && it.authenticated }
    if (isCiServer) {
      buildScan.value 'Branch', System.getenv()["GITHUB_REF_NAME"]
      buildScan.value 'GitHub Actions Run', "https://github.com/${System.getenv('GITHUB_REPOSITORY')}/actions/runs/${System.getenv('GITHUB_RUN_ID')}"
    }
  }
}

buildCache {
  local {
    enabled = !isCiServer
  }
  remote(develocity.buildCache) {
    push = isCiServer
  }
}


// use Settings.getRootDir() so that it doesn't matter which directory you are executing from
File extraSettings = new File(rootDir, 'private/private-settings.gradle')
if (extraSettings.exists()) {
  apply from: extraSettings
}



