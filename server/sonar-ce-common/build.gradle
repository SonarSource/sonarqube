description = 'Code relating to the Compute Engine shared between the Compute Engine and the Web Server'

sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Compute Engine :: Common"
  }
}

sourceSets {
  test {
    resources {
      srcDirs += ['src/test/projects']
    }
  }
}


import org.apache.tools.ant.filters.ReplaceTokens
processResources {
  filesMatching('build.properties') {
    filter ReplaceTokens, tokens: [
      'buildNumber': release ? 'git rev-parse HEAD'.execute().text.trim() : 'N/A'
    ]
  }
}

configurations {
  testImplementation.extendsFrom compileOnly
}

dependencies {
  // please keep the list grouped by configuration and ordered by name

  implementation 'com.google.guava:guava'
  implementation 'org.slf4j:jul-to-slf4j'
  implementation 'org.slf4j:slf4j-api'
  implementation project(':server:sonar-ce-task')
  implementation project(':server:sonar-server-common')
  implementation project(':sonar-core')
  implementation project(path: ':sonar-plugin-api', configuration: 'shadow')

  compileOnly 'com.google.code.findbugs:jsr305'

  testImplementation 'com.google.code.findbugs:jsr305'
  testImplementation 'com.tngtech.java:junit-dataprovider'
  testImplementation 'commons-lang:commons-lang'
  testImplementation 'junit:junit'
  testImplementation 'org.apache.logging.log4j:log4j-api'
  testImplementation 'org.apache.logging.log4j:log4j-core'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'org.assertj:assertj-guava'
  testImplementation 'org.hamcrest:hamcrest-all'
  testImplementation project(':sonar-plugin-api-impl')
  testImplementation testFixtures(project(':server:sonar-server-common'))
}
