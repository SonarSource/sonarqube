description = 'SonarQube Spring shared configuration and utilities'

sonar {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Server :: Spring"
  }
}

dependencies {
  // please keep the list grouped by configuration and ordered by name
  api 'com.fasterxml.jackson.core:jackson-annotations'
  api 'com.google.code.findbugs:jsr305'
  api 'com.google.guava:guava'
  api 'org.hibernate.validator:hibernate-validator'
  api 'org.slf4j:slf4j-api'
  api 'org.springframework:spring-context'
  api 'org.springframework:spring-webmvc'


  compileOnlyApi 'jakarta.servlet:jakarta.servlet-api'

  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'org.mockito:mockito-core'
  testImplementation 'org.mockito:mockito-junit-jupiter'
  testImplementation 'org.springframework:spring-test'
  testImplementation 'com.squareup.okhttp3:mockwebserver'

  testFixturesApi 'org.springframework:spring-test'

  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

compileJava {
  options.compilerArgs += [
    '--add-reads', 'org.sonar.server.spring=ALL-UNNAMED'
  ]
}

test {
  // Enabling the JUnit Platform (see https://github.com/junit-team/junit5-samples/tree/master/junit5-migration-gradle)
  useJUnitPlatform()
}

javadoc {
  exclude 'module-info.java'
  doFirst {
    options.classpath = classpath.files.toList()
  }
}

artifactoryPublish.skip = !deployCommunity
publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      if (release) {
        artifact sourcesJar
        artifact javadocJar
      }
    }
  }
}

