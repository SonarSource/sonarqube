apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'

configurations {
  search {
    transitive = false
  }
  ce {
    transitive = false
  }
  scanner
  webapp
  jdbc_mssql {
    transitive = false
  }
  jdbc_mysql
  jdbc_postgresql
  jdbc_h2
  plugin {
    transitive = false
  }
  wrapper
}

dependencies {
  compile project(':server:sonar-process')
  compile project(':server:sonar-process-monitor')

  compile 'com.hazelcast:hazelcast'
  // TODO all below are provided
//  compile 'com.google.code.findbugs:jsr305'
  compile 'org.elasticsearch:elasticsearch'
  compileOnly project(':server:sonar-server') // needed for compilation, others for packaging
  compile 'net.java.dev.jna:jna' // TOOD not in pom
  search project(':server:sonar-search')
  ce 'org.nanohttpd:nanohttpd' // TODO not in pom
  ce project(':server:sonar-ce')

  scanner project(path: ':sonar-scanner-engine-shaded', configuration: 'shadow')

  webapp project(path: ':server:sonar-web', configuration: 'archives')

  jdbc_mssql 'com.microsoft.sqlserver:mssql-jdbc'
  jdbc_mysql 'mysql:mysql-connector-java'
  jdbc_postgresql 'org.postgresql:postgresql'
  jdbc_h2 'com.h2database:h2'

  plugin 'org.sonarsource.java:sonar-java-plugin:4.5.0.8398'
  plugin 'org.sonarsource.javascript:sonar-javascript-plugin:2.20.0.4207'
  plugin 'org.sonarsource.dotnet:sonar-csharp-plugin:5.7.0.612'
  plugin 'org.sonarsource.scm.git:sonar-scm-git-plugin:1.2'
  plugin 'org.sonarsource.scm.svn:sonar-scm-svn-plugin:1.4.0.522'
  plugin 'org.sonarsource.php:sonar-php-plugin:2.9.2.1744'
  plugin 'org.sonarsource.python:sonar-python-plugin:1.7.0.1195'
  plugin 'org.sonarsource.flex:sonar-flex-plugin:2.3'

  wrapper 'tanukisoft:wrapper:3.2.3'

  testCompile 'junit:junit'
  testCompile 'org.assertj:assertj-core'
  testCompile 'org.mockito:mockito-core'
}

jar {
  classifier = 'original'
}
shadowJar {
  manifest {
    attributes 'Main-Class': 'org.sonar.application.App'
  }
  classifier = null
}

task zip(type: Zip) {
  into('sonarqube/') {
    from file('src/main/assembly')
  }
  into('sonarqube/lib/') {
    from shadowJar
  }
  into('sonarqube/lib/jsw/') {
    from configurations.wrapper
  }
  into('sonarqube/lib/bundled-plugins/') {
    from configurations.plugin
  }
  into('sonarqube/lib/common/') {
    from configurations.compile
  }
  into('sonarqube/lib/server/') {
    from configurations.compileOnly - configurations.compile
  }
  into('sonarqube/lib/search/') {
    from configurations.search
  }
  into('sonarqube/lib/ce/') {
    from configurations.ce
  }
  into('sonarqube/lib/scanner/') {
    from configurations.scanner
  }
  dependsOn configurations.webapp
  into('sonarqube/web/') {
    from {
      configurations.webapp.files.collect { zipTree(it) }
    }
  }
  into('sonarqube/lib/jdbc/mssql/') {
    from configurations.jdbc_mssql
  }
  into('sonarqube/lib/jdbc/mysql/') {
    from configurations.jdbc_mysql
  }
  into('sonarqube/lib/jdbc/postgresql/') {
    from configurations.jdbc_postgresql
  }
  into('sonarqube/lib/jdbc/h2/') {
    from configurations.jdbc_h2
  }
}

artifacts {
  archives zip
}

install.repositories.mavenInstaller.pom.whenConfigured {pom ->
  pom.dependencies = []
}
