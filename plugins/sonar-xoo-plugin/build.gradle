configurations {
  testImplementation.extendsFrom(compileOnly)
}

configureCompileJavaToVersion 8

dependencies {
  implementation 'com.google.guava:guava'
  implementation 'commons-io:commons-io'
  implementation 'commons-lang:commons-lang'
  implementation 'org.apache.commons:commons-csv'
  compileOnly project(path: ':sonar-plugin-api', configuration: 'shadow')
  compileOnly 'com.google.code.findbugs:jsr305'

  testImplementation 'junit:junit'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'org.mockito:mockito-core'
  testImplementation project(':sonar-plugin-api-impl')
}

jar {
  manifest {
    attributes(
      'Plugin-Key': 'xoo',
      'Plugin-Version': project.version,
      'Plugin-Class': 'org.sonar.xoo.XooPlugin',
      'Plugin-ChildFirstClassLoader': 'false',
      'Sonar-Version': project.version,
      'SonarLint-Supported': 'true',
      'Plugin-Name': 'Xoo'
    )
  }
  into('META-INF/lib') {
    from configurations.compileClasspath
  }
}

artifactoryPublish.skip = false

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      if (release) {
        artifact sourcesJar
        artifact javadocJar
      }
    }
  }
}
