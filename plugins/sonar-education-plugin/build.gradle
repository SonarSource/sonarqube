configurations {
    testImplementation.extendsFrom(compileOnlyApi)
}

dependencies {
    compileOnlyApi 'org.sonarsource.api.plugin:sonar-plugin-api'
    compileOnlyApi 'com.github.spotbugs:spotbugs-annotations'
    implementation 'commons-io:commons-io'

    testImplementation 'junit:junit'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation project(':sonar-plugin-api-impl')
}

jar {
    manifest {
        attributes(
                'Plugin-Key': 'education',
                'Plugin-Version': project.version,
                'Plugin-Class': 'org.sonar.education.EducationPlugin',
                'Plugin-ChildFirstClassLoader': 'false',
                'Sonar-Version': project.pluginApiVersion,
                'SonarLint-Supported': 'true',
                'Plugin-Name': 'Education'
        )
    }
    into('META-INF/lib') {
        from configurations.runtimeClasspath
    }
}

artifactoryPublish.skip = !deployCommunity

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            if (release) {
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}
