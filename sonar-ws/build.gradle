plugins {
    id 'java-library'
}

sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Web Service"
    property 'sonar.exclusions', 'src/main/java/org/sonarqube/ws/client/*/*.java'
  }
}

configureCompileJavaToVersion 8

configurations {
  testImplementation.extendsFrom(compileOnly)
}

dependencies {
  // please keep list ordered

  implementation 'com.google.protobuf:protobuf-java'
  api 'com.squareup.okhttp3:okhttp'
  implementation 'com.google.code.gson:gson'

  compileOnly 'com.google.code.findbugs:jsr305'
  compileOnly 'javax.annotation:javax.annotation-api'
  compileOnly project(path: ':sonar-plugin-api', configuration: 'shadow')

  testImplementation 'com.squareup.okhttp3:mockwebserver'
  testImplementation 'com.squareup.okio:okio'
  testImplementation 'commons-io:commons-io'
  testImplementation 'commons-lang:commons-lang'
  testImplementation 'junit:junit'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'org.hamcrest:hamcrest-core'
  testImplementation 'org.mockito:mockito-core'
  testImplementation project(':sonar-testing-harness')
}

artifactoryPublish.skip = false

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      if (release) {
        artifact sourcesJar
        artifact javadocJar
      }
    }
  }
}
